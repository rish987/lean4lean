import Lean.Environment

namespace Lean.Kernel.Environment

def L4L.SortType : Expr := .forallE `l (.const `L4L.Level []) (.app (.const `L4L.Sort []) (.app (.const `L4L.Level.succ []) (.bvar 0))) .default

def L4L.SortDef : ConstantInfo :=
  .axiomInfo {name := `L4L.Sort, levelParams := [], type := L4L.SortType , isUnsafe := false}

def get (env : Kernel.Environment) (n : Name) : Except KernelException ConstantInfo := do
  if n == `L4L.Sort then
    return L4L.SortDef
  match env.find? n with
  | some ci => pure ci
  | none => throw <| .unknownConstant env n

def checkDuplicatedUnivParams : List Name → Except KernelException Unit
  | [] => pure ()
  | p :: ls => do
    if p ∈ ls then
      throw <| .other
        s!"failed to add declaration to environment, duplicate universe level parameter: '{p}'"
    checkDuplicatedUnivParams ls

def checkNoMVar (env : Kernel.Environment) (n : Name) (e : Expr) : Except KernelException Unit := do
  if e.hasMVar then
    throw <| .declHasMVars env n e

def checkNoFVar (env : Kernel.Environment) (n : Name) (e : Expr) : Except KernelException Unit := do
  if e.hasFVar then
    throw <| .declHasFVars env n e

def checkNoMVarNoFVar (env : Kernel.Environment) (n : Name) (e : Expr) : Except KernelException Unit := do
  checkNoMVar env n e
  checkNoFVar env n e

def primitives : NameSet := .ofList [
  ``Bool, ``Bool.false, ``Bool.true,
  ``Nat, ``Nat.zero, ``Nat.succ,
  ``Nat.add, ``Nat.pred, ``Nat.sub, ``Nat.mul, ``Nat.pow,
  ``Nat.gcd, ``Nat.mod, ``Nat.div, ``Nat.beq, ``Nat.ble,
  ``String, ``String.mk]

def checkName (env : Kernel.Environment) (n : Name)
    (allowPrimitive := false) : Except KernelException Unit := do
  if env.constants.contains n then
    throw <| .alreadyDeclared env n
  unless allowPrimitive do
    if primitives.contains n then
      throw <| .other s!"unexpected use of primitive name {n}"
